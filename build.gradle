plugins {
    id 'java'
    id 'jacoco'
    id 'pmd'
    id 'checkstyle'
    id 'com.github.spotbugs' version '6.1.3'
    id 'com.microsoft.azure.azurefunctions' version '1.12.0'
}

import com.github.spotbugs.snom.SpotBugsTask

group 'com.alx.sftp'
version '1.0.0'

description = "ALX SFTP Test Function App"

apply plugin: "java"
apply plugin: "com.microsoft.azure.azurefunctions"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.microsoft.azure.functions:azure-functions-java-library:3.0.0'
    implementation 'com.azure:azure-identity:1.10.3'
    implementation 'com.azure.resourcemanager:azure-resourcemanager:2.37.0'
    implementation 'com.azure.resourcemanager:azure-resourcemanager-storage:2.37.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.7.2'
    testImplementation 'com.microsoft.azure.functions:azure-functions-java-library:3.0.0'
    testImplementation 'org.powermock:powermock-api-mockito2:2.0.9'
    testImplementation 'org.powermock:powermock-module-junit4:2.0.9'
    testImplementation 'org.mockito:mockito-core:3.11.2'

    implementation 'net.sourceforge.pmd:pmd:7.10.0'

    compileOnly 'com.github.spotbugs:spotbugs-annotations:4.9.0'
    compileOnly 'com.github.spotbugs:spotbugs:4.8.6'

    spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.13.0'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.release = 17
}

test {
    useJUnitPlatform()
    testLogging.showStandardStreams = true
    testLogging {
        events = ["passed", "failed", "skipped"]
        exceptionFormat = "full"
    }
    finalizedBy jacocoTestReport, jacocoTestCoverageVerification
}

tasks.withType(Test).configureEach {
    jvmArgs("-XX:+EnableDynamicAgentLoading")
}

pmd {
    toolVersion = '7.10.0'
    consoleOutput = true
    ignoreFailures = true
    ruleSets = []
    ruleSetConfig = rootProject.resources.text.fromFile('config/pmd/ruleset.xml')
    sourceSets = [sourceSets.main]
    consoleOutput = true
}

checkstyle {
    toolVersion = "10.18.2"
    ignoreFailures = true
    maxWarnings = 0
    config = rootProject.resources.text.fromFile('config/checkstyle/checkstyle.xml')
    showViolations = true
    checkstyleTest.enabled = false
    checkstyleMain {
        excludes = [
                '**/generated/**'
        ]
    }
}

spotbugs {
    toolVersion = '4.8.6'
    ignoreFailures = true
    showProgress = true
    excludeFilter = file('config/spotbugs/exclude.xml')
}

tasks.withType(SpotBugsTask).configureEach {
    reports {
        xml.required.set(true)
        html.required.set(true)
    }
}

jacocoTestReport {
    dependsOn test
    executionData(fileTree(project.layout.buildDirectory).include("jacoco/*.exec"))
    reports {
        xml.required.set(true)
        html.required.set(true)
    }
    excludedClassFilesForReport(classDirectories)
}

jacocoTestCoverageVerification {
    executionData(fileTree(project.layout.buildDirectory).include("jacoco/*.exec"))
    violationRules {
        rule {
            failOnViolation = true
            limit {
                counter = 'LINE'
                minimum = 0.08
            }
            limit {
                counter = 'BRANCH'
                minimum = 0.07
            }
            excludedClassFilesForReport(classDirectories)
        }
    }
}

tasks.withType(Copy).configureEach {
    duplicatesStrategy 'exclude'
}

check.dependsOn(pmdMain, spotbugsMain, checkstyleMain, jacocoTestCoverageVerification)

private excludedClassFilesForReport(classDirectories) {
    classDirectories.setFrom(files(classDirectories.files.collect {
        fileTree(dir: it, exclude: [
                "**/**/configs/*",
                "**/**/exceptions/*",
                "**/**/dto/*",
                "**/**/cosmos/*"
        ])
    }))
}

azurefunctions {
    resourceGroup = System.getenv('resourceGroup')
    appName = System.getenv('appName') ?: rootProject.name

    auth {
        type = 'publish_profile'
        publishProfile = System.getenv('FUNCTIONAPP_PUBLISH_PROFILE')
    }

    localDebug = "transport=dt_socket,server=y,suspend=n,address=5005"
}
